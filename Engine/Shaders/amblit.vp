in vec4 in_position : POSITION;
in vec4 in_color : COLOR;
in vec3 in_normal : NORMAL;
in vec2 in_st : TEXCOORD0;
in vec4 in_tangent : TEXCOORD1;

out vec4 v2f_color;
out vec2 v2f_tcDiffuseBump;
out vec2 v2f_tcLightmap;
out vec3 v2f_viewVector;
out vec3 v2f_lightVector;
out vec4 v2f_lightProjection;
out vec3 v2f_normal;
out vec3 v2f_tangentToWorldMatrixS;
out vec3 v2f_tangentToWorldMatrixT;
out vec3 v2f_tangentToWorldMatrixR;

#ifdef INSTANCED_DRAW
	struct entityData_t {
		vec4 worldMatrixS;
		vec4 worldMatrixT;
		vec4 worldMatrixR;
		vec3 localViewOrigin;
		vec3 localLightDir;
		vec4 textureMatrixS;
		vec4 textureMatrixT;
		vec4 vertexColorScale;
		vec4 vertexColorAdd;
		vec4 constantColor;
	};

	uniform entityData_t entityData[256];
	#define ED entityData[gl_InstanceIDARB]
#else
	uniform vec4 worldMatrixS;
	uniform vec4 worldMatrixT;
	uniform vec4 worldMatrixR;
	uniform vec3 localViewOrigin;
	uniform vec3 localLightDir;
	uniform vec4 textureMatrixS;
	uniform vec4 textureMatrixT;
	uniform vec4 vertexColorScale;
	uniform vec4 vertexColorAdd;
	uniform vec4 constantColor;
#endif

uniform mat4 modelViewProjectionMatrix;

void main() {
#if _NORMAL_SOURCE != 0
	vec4 localVertex;
	vec3 localNormal;
	vec3 localTangent;
	vec3 localBiTangent;

	#ifdef COMPRESSED_VERTEX_NORMAL_TANGENTS
		vec3 vNormal = in_normal.xyz * 2.0 - 1.0;
        vec4 vTangent = in_tangent * 2.0 - 1.0;
	#else
		vec3 vNormal = in_normal.xyz;
        vec4 vTangent = in_tangent;
	#endif

	#ifdef GPU_SKINNING
		skinningMatrix(in_position, localVertex, vNormal, localNormal, vTangent, localTangent, localBiTangent);
	#else
		localVertex = in_position;
		localNormal = vNormal;
		localTangent = vTangent.xyz;
		localBiTangent = normalize(cross(vNormal, vTangent.xyz) * vTangent.w);
	#endif
#else
	vec4 localVertex;
	vec3 localNormal;

	#ifdef COMPRESSED_VERTEX_NORMAL_TANGENTS
		vec3 vNormal = in_normal.xyz * 2.0 - 1.0;
	#else
		vec3 vNormal = in_normal.xyz;
	#endif
	
	#ifdef GPU_SKINNING
		skinningMatrix(in_position, localVertex, vNormal, localNormal);
	#else
		localVertex = in_position;
		localNormal = vNormal;
	#endif
#endif

#if _NORMAL_SOURCE != 0
	mat3 TBN = mat3(localTangent, localBiTangent, localNormal);

	vec3 V = localViewOrigin.xyz - localVertex.xyz;

	#if !defined(V2F_VIEW_VECTOR) && defined(_PARALLAX)
		v2f_viewVector = V * TBN;
	#endif

	#ifndef NO_AMBIENT_CUBE_MAP
		v2f_tangentToWorldMatrixS.xyz = worldMatrixS.xyz * TBN;
		v2f_tangentToWorldMatrixT.xyz = worldMatrixT.xyz * TBN;
		v2f_tangentToWorldMatrixR.xyz = worldMatrixR.xyz * TBN;
		//v2f_tangentToWorldMatrixT.w = in_tangent.w;
	#endif
#else
	vec3 V = localViewOrigin.xyz - localVertex.xyz;

	v2f_normal = localNormal;

	#if !defined(V2F_VIEW_VECTOR) && defined(_PARALLAX)
		v2f_viewVector = V;
	#endif

	#ifndef NO_AMBIENT_CUBE_MAP
		v2f_tangentToWorldMatrixS.xyz = worldMatrixS.xyz;
		v2f_tangentToWorldMatrixT.xyz = worldMatrixT.xyz;
		v2f_tangentToWorldMatrixR.xyz = worldMatrixR.xyz;
	#endif
#endif

	v2f_tcDiffuseBump.x = dot(textureMatrixS, vec4(in_st, 0.0, 1.0));
	v2f_tcDiffuseBump.y = dot(textureMatrixT, vec4(in_st, 0.0, 1.0));

#ifdef LIGHTMAP
	v2f_tcLightmap = in_st2.xy;
#endif

	v2f_color = (in_color * vertexColorScale + vertexColorAdd) * constantColor;

	gl_Position = modelViewProjectionMatrix * localVertex;
}
